plugins {
    id 'multiloader-common'
    alias libs.plugins.modDevGradle
}

neoForge {
    neoFormVersion = libs.neoForm.get().version
    // Automatically enable AccessTransformers if the file exists
    def at = file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment.parchmentArtifact = libs.parchment.get().toString()
}

dependencies {
    compileOnly libs.mixin
}

apply from: rootProject.file('repositories.gradle')
apply from: 'dependencies.gradle'

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
    commonGeneratedResources {
        canBeResolved = false
        canBeConsumed = true
    }
}

sourceSets {
    generated {
        resources {
            srcDir 'src/generated/resources'
        }
    }
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    commonResources sourceSets.main.resources.sourceDirectories.singleFile
    commonGeneratedResources sourceSets.generated.resources.sourceDirectories.singleFile
}
