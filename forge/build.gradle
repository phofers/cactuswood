import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'multiloader-loader'
    alias libs.plugins.forgeGradle
    alias libs.plugins.mixin
    alias libs.plugins.curseForgeGradle
    alias libs.plugins.modrinthMinotaur
}

mixin {
    config("${mod_id}.mixins.json")
    config("${mod_id}.forge.mixins.json")
}

jar {
    manifest {
        attributes["MixinConfigs"] = "${mod_id}.mixins.json,${mod_id}.forge.mixins.json"
    }
}

minecraft {
    mappings channel: 'official', version: libs.minecraft.get().version

    copyIdeResources = true //Calls processResources when in dev

    reobf = false // Forge 1.20.6+ uses official mappings at runtime, so we shouldn't reobf from official to SRG

    // Automatically enable forge AccessTransformers if the file exists
    def at = file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformer = at
    }

    runs {
        client {
            workingDirectory file('runs/client')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName "Client"

            property 'forge.enabledGameTestNamespaces', mod_id

            mods {
                modClientRun {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory file('runs/server')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName "Server"

            property 'forge.enabledGameTestNamespaces', mod_id

            mods {
                modServerRun {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

dependencies {
    def mcVersion = libs.minecraft.get().version
    def forgeVersion = libs.forge.get().version
    minecraft "net.minecraftforge:forge:${mcVersion}-${forgeVersion}"
    def mixinVersion = libs.mixin.get().version
    annotationProcessor "org.spongepowered:mixin:${mixinVersion}:processor"
}

apply from: rootProject.file('repositories.gradle')
apply from: 'dependencies.gradle'

publishing {
    publications {
        mavenJava(MavenPublication) {
            fg.component(it)
        }
    }
}

tasks.register('curseforge', TaskPublishCurseForge) {
    dependsOn('build')
    description = 'Publishes the Forge build to CurseForge.'
    group = 'publishing'

    apiToken = project.findProperty("curseforge.api_key") ?: System.getenv("CURSEFORGE_TOKEN") ?: "none"

    def projectId = findProperty("curseforge_project_id")
    onlyIf {
        projectId != null
    }
    if (projectId) {
        def mainFile = upload(findProperty("curseforge_project_id"), file("${project.buildDir}/libs/${base.archivesName}-${version}.jar"))
        mainFile.changelog = rootProject.file('CHANGELOG.md').text
        mainFile.addRequirement("balm")
        mainFile.addGameVersion(libs.minecraft.get().version)
        mainFile.releaseType = "release"
        project.findProperty("curseforge_environments")?.split(",")?.toList()?.each { mainFile.addEnvironment(it) }
    }
}

modrinth {
    token = project.findProperty("modrinth.token") ?: System.getenv("MODRINTH_TOKEN") ?: "none"
    projectId = findProperty("modrinth_project_id")
    versionType = "release"
    versionNumber = project.version + "+forge-" + libs.minecraft.get().version
    uploadFile = jar
    def changelogFile = rootProject.file("CHANGELOG.md")
    if (changelogFile.exists()) {
        changelog = changelogFile.text
    }
    gameVersions = [libs.minecraft.get().version]
    loaders = ['forge']
    dependencies {
        required.project "balm"
    }
}

sourceSets.each {
    def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
    it.output.resourcesDir = dir
    it.java.destinationDirectory = dir
}
