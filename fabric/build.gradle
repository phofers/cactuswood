import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'multiloader-loader'
    alias libs.plugins.fabricLoom
    alias libs.plugins.curseForgeGradle
    alias libs.plugins.modrinthMinotaur
}

dependencies {
    minecraft libs.minecraft
    mappings loom.layered() {
        officialMojangMappings()
        parchment variantOf(libs.parchment) {
            artifactType('zip')
        }
    }
    modImplementation libs.fabricLoader
    modImplementation libs.fabricApi
}

apply from: rootProject.file('repositories.gradle')
apply from: 'dependencies.gradle'

loom {
    def aw = project(":common").file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }

    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }

    runs {
        client {
            client()
            setConfigName("fabric Client")
            ideConfigGenerated(true)
            runDir("runs/client")
        }
        server {
            server()
            setConfigName("fabric Server")
            ideConfigGenerated(true)
            runDir("runs/server")
        }
        data {
            inherit client
            setConfigName("fabric Data")
            ideConfigGenerated(true)
            runDir("build/datagen")

            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${project(":common").file("src/generated/resources")}"
            vmArg "-Dfabric-api.datagen.modid=${mod_id}"
        }
    }
}

tasks.register('curseforge', TaskPublishCurseForge) {
    dependsOn('build')
    description = 'Publishes the Fabric build to CurseForge.'
    group = 'publishing'

    apiToken = project.findProperty("curseforge.api_key") ?: System.getenv("CURSEFORGE_TOKEN") ?: "none"

    def projectId = findProperty("curseforge_project_id")
    onlyIf {
        projectId != null
    }
    if (projectId) {
        def mainFile = upload(findProperty("curseforge_project_id"), remapJar.archiveFile.get().asFile)
        mainFile.changelog = rootProject.file('CHANGELOG.md').text
        mainFile.addRequirement("fabric-api")
        mainFile.addRequirement("balm")
        mainFile.addGameVersion(libs.minecraft.get().version)
        mainFile.releaseType = "release"
        project.findProperty("curseforge_environments")?.split(",")?.toList()?.each { mainFile.addEnvironment(it) }
    }
}

modrinth {
    token = project.findProperty("modrinth.token") ?: System.getenv("MODRINTH_TOKEN") ?: "none"
    projectId = findProperty("modrinth_project_id")
    versionType = "release"
    versionNumber = project.version + "+fabric-" + libs.minecraft.get().version
    uploadFile = remapJar
    def changelogFile = rootProject.file("CHANGELOG.md")
    if (changelogFile.exists()) {
        changelog = changelogFile.text
    }
    gameVersions = [libs.minecraft.get().version]
    loaders = ['fabric']
    dependencies {
        required.project "fabric-api"
        required.project "balm"
    }
}